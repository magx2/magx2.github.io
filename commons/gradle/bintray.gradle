apply plugin: "com.jfrog.bintray"
apply plugin: 'maven'
apply plugin: 'maven-publish'

bintray {
	def conf = new BintrayConfig(bintrayConfig, project)
	user = conf.user
	key = conf.key

	publications = conf.publications
	dryRun = project.hasProperty('dryRun')
	publish = true
	pkg {
		repo = conf.repo
		name = conf.groupId
		userOrg = conf.userOrg
		desc = conf.desc
		websiteUrl = conf.websiteUrl
		issueTrackerUrl = conf.issueTrackerUrl
		vcsUrl = conf.vcsUrl
		licenses = conf.licenses
		labels = conf.labels
		publicDownloadNumbers = conf.publicDownloadNumbers

		githubRepo = conf.githubRepo
		githubReleaseNotesFile = conf.githubReleaseNotesFile

		//noinspection GroovyAssignabilityCheck
		version {
			name = conf.versionName
			vcsTag = conf.versionVcsTag
			gpg {
				sign = true
				passphrase = conf.passphrase
			}
		}
	}
}

publishing {
	def conf = new BintrayConfig(bintrayConfig, project)
	publications {
		BintrayProjectPublication(MavenPublication) {
			from components.java
			groupId conf.groupId
			artifactId conf.name
			version conf.versionName

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

class BintrayConfig {
	final project

	final String user
	final String key

	final List<String> publications
	final String repo
	final String groupId
	final String name
	final String userOrg
	final String desc
	final String websiteUrl
	final String issueTrackerUrl
	final String vcsUrl
	final List<String> licenses
	final List<String> labels
	final boolean publicDownloadNumbers
	final String githubRepo
	final String githubReleaseNotesFile

	final String versionName
	final String versionVcsTag

	final String passphrase

	BintrayConfig(def params, def project) {
		this.project = project

		this.user = params.user ?: System.getenv('BINTRAY_USER')
		this.key = params.key ?: System.getenv('BINTRAY_KEY')

		this.publications = params.publications ?: ['BintrayProjectPublication']
		this.repo = Objects.requireNonNull(params.repo, "repo")
		this.groupId = params.groupId ?: project.group
		this.name = params.name ?: project.name
		this.userOrg = Objects.requireNonNull(params.userOrg, "userOrg")
		this.desc = Objects.requireNonNull(params.desc, "desc")
		this.websiteUrl = Objects.requireNonNull(params.websiteUrl, "websiteUrl")
		this.issueTrackerUrl = Objects.requireNonNull(params.issueTrackerUrl, "issueTrackerUrl")
		this.vcsUrl = Objects.requireNonNull(params.vcsUrl, "vcsUrl")
		this.licenses = Objects.requireNonNull(params.licenses, "licenses")
		this.labels = params.labels ?: []
		this.publicDownloadNumbers = params.publicDownloadNumbers
		this.githubRepo = Objects.requireNonNull(params.githubRepo,"githubRepo")
		this.githubReleaseNotesFile = params.githubReleaseNotesFile ?: "README.md"

		this.versionName = parserVersionName(params.versionName)
		this.versionVcsTag = parserVersionTag(params.versionVcsTag)

		this.passphrase = parsePassphrase(params.passphrase)
	}
	
	private String parserVersionName(String versionName) {
		return (versionName ?: project.version).replaceAll("-SNAPSHOT", '')
	}

	private String parserVersionTag(String versionTag) {
		return 'v' + (versionTag ?: project.version).replaceAll("-SNAPSHOT", '')
	}

	private String parsePassphrase(String passphrase) {
		def defaultPassphrase = "${project.name}Passphrase"
		if (passphrase) {
			return passphrase
		} else if (project.hasProperty(defaultPassphrase)) {
			return project.property(defaultPassphrase)
		} else {
			return System.getenv(defaultPassphrase)
		}
	}

}